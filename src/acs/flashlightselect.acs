#library "flashlightselect"
#include "zcommon.acs"

#define FLASHLIGHT_OBJECT "Flashlight"
#define FLARE_OBJECT "Flare"
#define CVAR_SWAP_WEAPON "flashlight_lastweap"
#define CVAR_USE_FLARES "flashlight_auto_use_flare"
#define CVAR_SHOW_MSG "flashlight_flare_show_count"

script "FlashlightStart" ENTER
{
    str lastweap = GetCVarString(CVAR_SWAP_WEAPON);
    str weap = GetWeapon();
    
    bool selected_flashlight = weap == FLASHLIGHT_OBJECT;
    
    if (!selected_flashlight)
        SetCVarString(CVAR_SWAP_WEAPON,weap);
}

script "UseFlare" (void) net
{
    if (GetCVar(CVAR_SHOW_MSG))
    {
        str printstring;
        int flares = CheckInventory(FLARE_OBJECT);
        if (flares)
            printstring = StrParam(d:flares - 1,s:" ",l:"FLASHLIGHT_FLARES_REMAIN");
        else
            printstring = StrParam(l:"FLASHLIGHT_NO_FLARES_REMAIN");
        print(s:printstring);
    }
    UseInventory(FLARE_OBJECT);
}

script "ToggleFlashlight" (void) net
{
    str lastweap = GetCVarString(CVAR_SWAP_WEAPON);
    str weap = GetWeapon();
    
    bool has_flashlight = CheckInventory(FLASHLIGHT_OBJECT);
    bool has_flares = CheckInventory(FLARE_OBJECT);
    bool use_flares = GetCVar(CVAR_USE_FLARES) > 0;
    
    bool selected_flashlight = weap == FLASHLIGHT_OBJECT;
    bool selected_last = weap == lastweap;
    
    if (selected_flashlight)
    {
        SetWeapon(lastweap);
    }
    else if (has_flashlight && weap != FLASHLIGHT_OBJECT)
    {
        SetCVarString(CVAR_SWAP_WEAPON,weap);
        SetWeapon(FLASHLIGHT_OBJECT);
    }
    else if (has_flares && use_flares)
    {
        ACS_NamedExecute("UseFlare",0);
    }
    else if (!has_flares && use_flares)
    {
        Print(l:"FLASHLIGHT_NO_FLASHLIGHT_OR_FLARES");
    }
    else
    {
        Print(l:"FLASHLIGHT_NO_FLASHLIGHT");
    }
}
