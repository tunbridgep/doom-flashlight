class Flare : CustomInventory
{
    Default
    {
        Radius 4;
        Height 18;
        //+INVENTORY.INVBAR;
        +INVENTORY.HUBPOWER;
        +INVENTORY.PERSISTENTPOWER;
        Inventory.MaxAmount 30;
        Inventory.InterHubAmount 30;
        Inventory.Icon "FLARINV";
        Inventory.PickupMessage "You picked up a flare.";
    }
    
    //this would update in real-time, but the game is a bit weird when it comes to 
    //override void Tick()
    override void BeginPlay()
    {
        self.bINVBAR = FlareConfig.GetAppearInInventory();
    }
    
    States
    {
        Spawn:
            FLZR D -1;
            Stop;
        Use:
            TNT1 A 1 A_FireProjectile("ActiveFlareProjectile");
            Stop;
    }
}

class FlareBox : CustomInventory
{
    Default
    {
        Radius 28;
        Height 16;
        Inventory.PickupMessage "You picked up a box of flares.";
    }
    States
    {
        Spawn:
            FLZR C -1;
            Stop;
        Pickup:
            TNT1 A 1 A_GiveInventory ("Flare", 5);
            Stop;
    }
}

class FlareLight : PointLightFlicker
{
    Actor toFollow;
    Vector3 offset;
    
    override void BeginPlay()
    {
        Color color = FlareConfig.GetColor();
        args[LIGHT_RED] = color.r;
        args[LIGHT_GREEN] = color.g;
        args[LIGHT_BLUE] = color.b;
        SetIntensity(242,240);
        bATTENUATE = true;
    }
    
    //This is here so we can easily randomise it and make our fire effect look more
    //genuine
    protected void SetIntensity(int primary, int secondary)
    {
        args[LIGHT_INTENSITY] = primary;
        args[LIGHT_SECONDARY_INTENSITY] = secondary;
    }
    
    override void Tick()
    {
        ActorUtil.MoveToActor(self,toFollow,offset);
        int new_intensity = random(220,260);
        SetIntensity(new_intensity,new_intensity - 2);
        Super.Tick();
    }
}

class FlareBase : Actor abstract
{
    FlareLight pointLight;
    
    protected void GenerateDynamicLight()
    {
        if (!pointLight)
        {
            Debug.LogVerbose("Creating dynamic light for flare");
            pointLight = FlareLight(Actor.Spawn("FlareLight"));
            if (pointLight)
                pointLight.toFollow = self;
        }
    }
    
    protected void RemoveDynamicLight()
    {
        if (pointLight)
            pointLight.Destroy();
    }

    Default
    {
        Radius 22;
        Height 11;
    }
    States
    {
        Spawn:
            FLZR A 0;
            FLZR A 1
            {
                GenerateDynamicLight();
                A_PlaySoundEx("Flare/Loop", "SoundSlot7", 1, -1);
            }
            Goto PostSpawn;
        PostSpawn:    
            FLZR A 1 Bright;
        Flicker:
            FLZR AB random(1,4) Bright;
            Goto Flicker;
    }
}

class ActiveFlare : FlareBase
{
    int burntime;
    
    override void BeginPlay()
    {
        burntime = FlareConfig.GetBurnTime();
    }
    
    override void Tick()
    {
        burntime--;
        if (burntime < 0)
        {
            SetStateLabel("BurnedOut");
            bFORCEXYBILLBOARD = true;
            RemoveDynamicLight();
            A_StopSound(7);
        }
        Super.Tick();
    }
    
    States
    {
        BurnedOut:
            FLZR E -1;
            Stop;
    }
}
class ActiveFlareProjectile : FlareBase
{
   
    private void GenerateStationaryFlare()
    {
        let flare = ActiveFlare(Actor.Spawn("ActiveFlare",pos));
        if (flare && pointlight)
        {
            pointLight.toFollow = flare;
            flare.pointLight = pointLight;
        }
        self.Destroy();
    }

    Default
    {
        BounceType "Doom";
        Projectile;
        Speed 20;
        +MISSILE;
        -NOGRAVITY;
        -BLOODSPLATTER;
        -EXTREMEDEATH;
        +EXPLODEONWATER;
        +SKYEXPLODE;
        +BOUNCEONFLOORS;
        +BOUNCEONWALLS;
        +BOUNCEONCEILINGS;
        -NOTELEPORT;
        +THRUSPECIES;
        +THRUGHOST;
        Gravity 1.0;
        BounceFactor 0.25;
        WallBounceFactor 0.15;
    }
    
    States
    {
        Spawn:
            FLZR A 0;
            FLZR A 1 Bright
            {
                GenerateDynamicLight();
                A_PlaySound("Flare/Light");
            }
            Goto PostSpawn;
        Death:
            FLZR A 1 GenerateStationaryFlare();
            Stop;

    }
}
