class FlashlightBeam : Spotlight
{
    private PlayerPawn toFollow;
    private bool on;
    private vector3 offset;
    int performance_tics_elapsed;
    const performance_tics = 3;
    
    void FollowPlayer(PlayerPawn pp)
    {
        toFollow = pp;
    }
    
    private void SetParameters()
    {
        PlayerInfo info = players[toFollow.PlayerNumber()];
               
        Color flashlight_color = CVar.GetCVar("flashlight_color",info).GetString();
        int flashlight_intensity = CVar.GetCVar("flashlight_intensity",info).GetInt();

        float flashlight_inner = CVar.GetCVar("flashlight_inner_angle",info).GetFloat();
        float flashlight_outer = CVar.GetCVar("flashlight_outer_angle",info).GetFloat();
        
        self.SpotInnerAngle = flashlight_inner;
        self.SpotOuterAngle = flashlight_outer;
        
        args[0] = flashlight_color.r;
        args[1] = flashlight_color.g;
        args[2] = flashlight_color.b;
        args[3] = flashlight_intensity;
        
        offset = (0,0,toFollow.ViewHeight);
    }
    
    bool IsOn()
    {
        return on;
    }
    
    void TurnOn(Actor activator)
    {
        SetParameters();
        on = true;
        Activate(activator);
    }
    
    void TurnOff(Actor activator)
    {
        on = false;
        DeActivate(activator);
    }
    
    override void Tick()
    {
        if (on && toFollow)
        {
            A_SetAngle(toFollow.angle, 0);
            A_SetPitch(toFollow.Pitch, SPF_INTERPOLATE);
            
            vector3 pos = (toFollow.pos.x + offset.x, toFollow.pos.y + offset.y, toFollow.pos.z + offset.z);
            
            if (flashlight_performance_mode && performance_tics_elapsed < performance_tics)
            {
                performance_tics_elapsed++;
            }
            else
            {
                SetOrigin(pos, false);
                performance_tics_elapsed = 0;
            }
        }
        Super.Tick();
    }
}
